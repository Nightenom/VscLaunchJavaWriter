plugins {
    id 'java'
    id 'java-library'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    id 'net.neoforged.gradleutils'
}

repositories {
    mavenCentral()
    flatDir { dirs 'libs' }
}

dependencies {
    // api deps go into enduser classpath
    
    // implementation are local deps
    implementation 'com.google.code.gson:gson:2.10.1'

    testImplementation(platform("org.junit:junit-bom:5.10.0"))
    testImplementation 'org.junit.platform:junit-platform-console-standalone:1.10.0'
}

tasks.named('test', Test) {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
}

group = 'cz.nightenom'
version = gradleutils.getTagOffsetVersion()

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
java.withSourcesJar()

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Nightenom/VscLaunchJavaWriter")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}